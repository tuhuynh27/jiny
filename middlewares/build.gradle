plugins {
    id 'com.adarshr.test-logger' version '2.1.0'
}
configurations {
    extraLibs
}

dependencies {
    implementation project(':core')
    compileOnly 'io.jsonwebtoken:jjwt-api:0.11.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2',
                'io.jsonwebtoken:jjwt-jackson:0.11.2'

    // dependencies to be included in jar file
    extraLibs   'io.jsonwebtoken:jjwt-api:0.11.2',
                'io.jsonwebtoken:jjwt-impl:0.11.2',
                'io.jsonwebtoken:jjwt-jackson:0.11.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'middlewares'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Jiny Framework'
                description = 'Lightweight, modern, simple Java HTTP Server/Client for rapid development in the API era'
                url = 'https://jinyframework.com/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/huynhminhtufu/jiny/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'tuhuynh27'
                        name = 'Tu Huynh'
                        email = 'huynhminhtufu@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/huynhminhtufu/jiny.git'
                    developerConnection = 'scm:git:ssh://github.com:huynhminhtu/jiny.git'
                    url = 'https://github.com/huynhminhtufu/jiny/'
                }
            }
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

signing {
    sign publishing.publications.mavenJava
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
