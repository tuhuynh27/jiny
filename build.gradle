plugins {
    id 'idea'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'com.adarshr.test-logger' version '2.1.0'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

group 'com.jinyframework'
version '0.2.4'

sourceCompatibility = 1.8

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jiny'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Jiny Framework'
                description = 'Lightweight, modern, simple Java HTTP Server/Client for rapid development in the API era'
                url = 'https://jinyframework.com/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/huynhminhtufu/jiny/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'tuhuynh27'
                        name = 'Tu Huynh'
                        email = 'huynhminhtufu@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/huynhminhtufu/jiny.git'
                    developerConnection = 'scm:git:ssh://github.com:huynhminhtu/jiny.git'
                    url = 'https://github.com/huynhminhtufu/jiny/'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.findProperty("sonatypeUsername") ?: System.getenv("sonatypeUsernamer")
                password = project.findProperty("sonatypePassword") ?: System.getenv("sonatypePassword")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')

    // SLF4J as a facade
    compile group: 'org.slf4j', name:'slf4j-api', version: '1.7.2'

    // Implementations:
    compile group: 'ch.qos.logback', name:'logback-classic', version: '1.0.9'
    compile group: 'ch.qos.logback', name:'logback-core', version: '1.0.9'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

application {
    mainClassName = 'com.jinyframework.Test'
}
shadowJar {
    classifier = 'fat'
}